AC_PREREQ(2.13)

AC_INIT([Rgraphviz], 1.x, [biocore@stat.math.ethz.ch], [Rgraphviz])

AC_ARG_WITH(graphviz,
           [AS_HELP_STRING([--with-graphviz=DIR],
                           [root directory of graphviz installation (defaults to /usr/local)])],
           [if test ${with_graphviz} = "yes"; then
             GRAPHVIZ_DIR='/usr/local'
           else
             GRAPHVIZ_DIR=${with_graphviz}
           fi],
           GRAPHVIZ_DIR="")

dnl If user doesn't specify --with-graphviz arg, try pkg-config first
dnl and then try dotneato-config.
GRAPHVIZ_DEFAULT_DIR=""
if test -z "${GRAPHVIZ_DIR}" ; then
   AC_MSG_NOTICE(No --with-graphviz option was specified. Trying to find graphviz using other methods.)
   if test -z "${PKG_CONFIG}" ; then
      AC_PATH_PROG(PKG_CONFIG, pkg-config)
   fi
   if ! test -z "${PKG_CONFIG}" ; then
      GRAPHVIZ_CONFIG="$PKG_CONFIG libgvc"
      GRAPHVIZ_VERSTR="`${GRAPHVIZ_CONFIG} --modversion || echo ''`"
      if test -z "${GRAPHVIZ_VERSTR}" ; then
         AC_MSG_NOTICE(pkg-config was not able to find the graphviz library libgvc. This either indicates that graphviz is old or that something is wrong. Verify graphviz is installed and that PKG_CONFIG_PATH is correct.)
      else
         PKG_CPPFLAGS="`${GRAPHVIZ_CONFIG} --cflags`"
         PKG_LIBS="`${GRAPHVIZ_CONFIG} --libs`"
      fi
   fi

   if test -z "${GRAPHVIZ_VERSTR}" ; then
      AC_MSG_NOTICE(Using dotneato-config to find graphviz)
      if test -z "${DOTNEATO_CONFIG}" ; then
         AC_PATH_PROG(DOTNEATO_CONFIG, dotneato-config)
      fi
      if test -z "${DOTNEATO_CONFIG}" ; then
         AC_MSG_NOTICE(dotneato-config not found in PATH.)
         GRAPHVIZ_DEFAULT_DIR="/usr/local"
      else
         GRAPHVIZ_VERSTR=`${DOTNEATO_CONFIG} --version`
         PKG_CPPFLAGS="`${DOTNEATO_CONFIG} --cflags`"
         PKG_LIBS="`${DOTNEATO_CONFIG} --ldflags` `${DOTNEATO_CONFIG} --libs` -lm"
      fi
   fi
fi

if ! test -z "${GRAPHVIZ_DEFAULT_DIR}" ; then
   AC_MSG_NOTICE(Trying default graphviz dir ${GRAPHVIZ_DEFAULT_DIR}.  Consider specifying --with-graphviz)
   GRAPHVIZ_DIR=${GRAPHVIZ_DEFAULT_DIR}
fi
if ! test -z "${GRAPHVIZ_DIR}"; then
    if test -z "${GRAPHVIZ_DEFAULT_DIR}" ; then
        dnl User specified --with-graphviz DIR
        AC_MSG_NOTICE(Using user specified graphviz root directory ${GRAPHVIZ_DIR})
    else
        AC_MSG_NOTICE(Using graphviz root directory ${GRAPHVIZ_DIR})
    fi
    DOT="${GRAPHVIZ_DIR}/bin/dot"
    if ! test -x "${DOT}" ; then
       AC_MSG_ERROR($DOT not found.  Check graphviz installation.)
    else
       GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f3 -d" "`
       dnl Newer versions have different output from dot -V
       dnl this less than ideal, seems to work for graphviz 2.12
       if test "${GRAPHVIZ_VERSTR}" = "Graphviz"; then
                  GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f5 -d" "`
       fi
       if test "${GRAPHVIZ_VERSTR}" = "graphviz"; then
                  GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f5 -d" "`
       fi
       dnl We need to do version computation here
       dnl since the user-specified graphviz could be version 2.2.1
       dnl in which gase -lgvc will fail
       MAJOR=`echo ${GRAPHVIZ_VERSTR} | cut -f1 -d"."`
       MINOR=`echo ${GRAPHVIZ_VERSTR} | cut -f2 -d"."`
       if (test ${MAJOR} -eq "2" && test ${MINOR} -le "2") ; then
         PKG_CPPFLAGS="-I${GRAPHVIZ_DIR}/include/graphviz"
         PKG_LIBS="-L${GRAPHVIZ_DIR}/lib/graphviz -ldotneato -lm"
       else
         PKG_CPPFLAGS="-I${GRAPHVIZ_DIR}/include/graphviz"
         PKG_LIBS="-L${GRAPHVIZ_DIR}/lib/graphviz -L${GRAPHVIZ_DIR}/lib -lgvc"
       fi
    fi
fi

if test -z "${GRAPHVIZ_VERSTR}" ; then
   AC_MSG_ERROR(graphviz was not found.)
   exit 1
fi

dnl Verify we have a usable graphviz version.
dnl Currently, this means graphviz >= 2.2.x
AC_MSG_NOTICE(Found Graphviz version $GRAPHVIZ_VERSTR)
AC_MSG_NOTICE(Corresponding to major version $MAJOR and minor version $MINOR)

if (test ${MAJOR} -lt "2") || (test ${MAJOR} -eq "2" && test ${MINOR} -lt "2") ; then
   AC_MSG_ERROR("Need graphviz >= 2.2")
   exit 1
fi

AC_CONFIG_FILES([R/graphviz_build_version.R])
GRAPHVIZ_BUILD_VERSION=${GRAPHVIZ_VERSTR}
AC_MSG_NOTICE(Build Version = $GRAPHVIZ_BUILD_VERSION)

AC_MSG_NOTICE(We have inferred the following flags for graphviz)
AC_MSG_NOTICE(..PKG_CPPFLAGS=${PKG_CPPFLAGS})
AC_MSG_NOTICE(..PKG_LIBS=${PKG_LIBS})

GVIZ_DEFS=" -DGRAPHVIZ_MAJOR=${MAJOR} -DGRAPHVIZ_MINOR=${MINOR} "

AC_MSG_CHECKING([for whether compiler has bool])
AC_HEADER_STDBOOL
AC_TRY_COMPILE([
   #ifdef HAVE_STDBOOL_H
   #include <stdbool.h>
   #endif
   ],[
   bool foo = true, bar = false;
   ],[
   AC_MSG_RESULT(yes)
   GVIZ_DEFS="${GVIZ_DEFS} -DHAVE_STDBOOL_H=1 -DHAVE_BOOL=1"
   ],[
   AC_MSG_RESULT(no)])

AC_SUBST(GVIZ_DEFS)
AC_SUBST(PKG_CPPFLAGS)
AC_SUBST(PKG_LIBS)
AC_SUBST(GRAPHVIZ_BUILD_VERSION)
AC_OUTPUT(src/Makevars)
