AC_PREREQ(2.13)

AC_INIT([Rgraphviz], 1.x, [biocore@stat.math.ethz.ch], [Rgraphviz])

AC_HEADER_STDBOOL
AC_MSG_CHECKING([for whether compiler has bool])
AC_TRY_COMPILE([
   #ifdef HAVE_STDBOOL_H
   #include <stdbool.h>
   #endif
   ],[
   bool foo = true, bar = false;
   ],[
   AC_MSG_RESULT(yes)
   GVIZ_DEFS="${GVIZ_DEFS} -DHAVE_STDBOOL_H=1 -DHAVE_BOOL=1"
   ],[
   AC_MSG_RESULT(no)])

AC_ARG_WITH(graphviz,
           [AS_HELP_STRING([--with-graphviz=DIR],
                           [root directory of Graphviz installation (defaults to /usr/local)])],
           [if test ${with_graphviz} = "yes"; then
             GRAPHVIZ_DIR='/usr/local'
           else
             GRAPHVIZ_DIR=${with_graphviz}
           fi],
           GRAPHVIZ_DIR="")

dnl If user doesn't specify --with-graphviz arg, try pkg-config first
dnl and then try dotneato-config.

if test -z "${GRAPHVIZ_DIR}" ; then
   AC_MSG_NOTICE([No --with-graphviz option was specified. Trying to find Graphviz using other methods.])
   dnl First we try pkg-config
   if test -z "${PKG_CONFIG}" ; then
      AC_PATH_PROG(PKG_CONFIG, pkg-config)
   fi
   if ! test -z "${PKG_CONFIG}" ; then
      GRAPHVIZ_CONFIG="${PKG_CONFIG} libgvc"
      GRAPHVIZ_VERSTR="`${GRAPHVIZ_CONFIG} --modversion || echo ''`"
      if test -z "${GRAPHVIZ_VERSTR}" ; then
         AC_MSG_NOTICE([pkg-config was not able to find the Graphviz library libgvc. This either indicates that Graphviz is old or that something is wrong. Verify Graphviz is installed and that PKG_CONFIG_PATH is correct.])
      else
         PKG_CPPFLAGS="`${GRAPHVIZ_CONFIG} --cflags`"
         PKG_LIBS="`${GRAPHVIZ_CONFIG} --libs`"
	 GVIZ_FLAGS_SET="yes"
      fi
   fi
   dnl Now we try dotneato-config; the existence of dotneato-config implies
   dnl that Graphviz is pretty old...
   if test -z "${GRAPHVIZ_VERSTR}" ; then
      if test -z "${DOTNEATO_CONFIG}" ; then
         AC_PATH_PROG(DOTNEATO_CONFIG, dotneato-config)
      fi
      if test -z "${DOTNEATO_CONFIG}" ; then
         AC_MSG_NOTICE([dotneato-config not found in PATH.])
	 AC_MSG_NOTICE([Using default directory /usr/local, consider specifiying --with-graphviz])
         GRAPHVIZ_DIR="/usr/local"
      else
         GRAPHVIZ_VERSTR=`${DOTNEATO_CONFIG} --version`
         PKG_CPPFLAGS="`${DOTNEATO_CONFIG} --cflags`"
         PKG_LIBS="`${DOTNEATO_CONFIG} --ldflags` `${DOTNEATO_CONFIG} --libs` -lm"
	 GVIZ_FLAGS_SET="yes"
      fi
   fi
else
   AC_MSG_NOTICE([Using Graphviz dir '${GRAPHVIZ_DIR}'])
fi

dnl At this place we have either that GRAPHVIZ_VERSTR is set (if Graphviz was found
dnl using pkg-config ot dotneato-config or that GRAPHVIZ_DIR has been set and we 
dnl need to use 'dot -V' to get the version string

if test -z "${GRAPHVIZ_VERSTR}" ; then
   DOT="${GRAPHVIZ_DIR}/bin/dot"
   if ! test -x "${DOT}" ; then
      AC_MSG_ERROR([$DOT not found.  Check Graphviz installation.])
      exit 1
   else
      GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f3 -d" "`
            if test "${GRAPHVIZ_VERSTR}" = "Graphviz"; then
         GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f5 -d" "`
      fi
      if test "${GRAPHVIZ_VERSTR}" = "graphviz"; then
         GRAPHVIZ_VERSTR=`${DOT} -V 2>&1 | cut -f5 -d" "`
      fi
   fi
fi

dnl At this point GRAPHVIZ_VERSTR ought to exist, we do computations on it to infer MAJOR and MINOR version

if test -z ${GRAPHVIZ_VERSTR} ; then 
    AC_MSG_ERROR([Unable to determine Graphviz version. Report to Rgraphviz maintainer.])
    exit 1
fi

AC_MSG_NOTICE([Found Graphviz version '${GRAPHVIZ_VERSTR}'.])

MAJOR=`echo ${GRAPHVIZ_VERSTR} | cut -f1 -d"."`
MINOR=`echo ${GRAPHVIZ_VERSTR} | cut -f2 -d"."`
if test -z ${MAJOR} || test -z ${MINOR} ; then
   AC_MSG_ERROR([Unable to infer Graphviz major and minor version, report to Rgraphviz maintainer])
   exit 1
fi
AC_MSG_NOTICE([Graphviz major version is '${MAJOR}' and minor version is '${MINOR}'.])
if (test ${MAJOR} -lt "2") || (test ${MAJOR} -eq "2" && test ${MINOR} -lt "2") ; then
   AC_MSG_ERROR([Need Graphviz >= 2.2])
   exit 1
fi

dnl Infer PKG_LIBS and PKG_CPPFLAGS, but only if pkg_config was not used
if test -z ${GVIZ_FLAGS_SET} ; then
   if (test ${MAJOR} -eq "2" && test ${MINOR} -le "2") ; then
      PKG_CPPFLAGS="-I${GRAPHVIZ_DIR}/include/graphviz"
      PKG_LIBS="-L${GRAPHVIZ_DIR}/lib/graphviz -ldotneato -lm"
   else
      PKG_CPPFLAGS="-I${GRAPHVIZ_DIR}/include/graphviz"
      PKG_LIBS="-L${GRAPHVIZ_DIR}/lib/graphviz -L${GRAPHVIZ_DIR}/lib -lgvc"
   fi
fi

GVIZ_DEFS="${GVIZ_DEFS} -DGRAPHVIZ_MAJOR=${MAJOR} -DGRAPHVIZ_MINOR=${MINOR} "
AC_MSG_NOTICE([Using the following compilation and linking flags for Rgraphviz])
AC_MSG_NOTICE([   PKG_CPPFLAGS=${PKG_CPPFLAGS}])
AC_MSG_NOTICE([   PKG_LIBS=${PKG_LIBS}])
AC_MSG_NOTICE([   GVIZ_DEFS=${GVIZ_DEFS}])

dnl Fixing GRAPHVIZ_BUILD_VERSION
AC_CONFIG_FILES([R/graphviz_build_version.R])
GRAPHVIZ_BUILD_VERSION=${GRAPHVIZ_VERSTR}
AC_MSG_NOTICE([Setting Graphviz Build version to '${GRAPHVIZ_BUILD_VERSION}'.])


AC_SUBST(GVIZ_DEFS)
AC_SUBST(PKG_CPPFLAGS)
AC_SUBST(PKG_LIBS)
AC_SUBST(GRAPHVIZ_BUILD_VERSION)
AC_OUTPUT(src/Makevars)
